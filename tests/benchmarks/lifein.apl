⍝ Conway's Game of Life in APL without the use of nested arrays
⍝ Martin Elsman, 2014-11-10

⍝ Function computing the next generation of a board of life
life ← {
  rowsum ← {
    (¯1⌽⍵) + ⍵ + 1⌽⍵
  }
  neighbor ← {
    (rowsum ¯1⊖⍵) + (rowsum ⍵) + rowsum 1⊖⍵       
  }
  n ← neighbor ⍵
  (n=3) ∨ (n=4) ∧ ⍵
}

lifepr ← {
⍝   ⎕ ← life ⍵
  life ⍵
}
 
nlife ← {
  (lifepr ⍣ ⍺) ⍵
}

⍝ input as generated by ./rle --flat < file.in > file.out

dim ← ReadCSVInt 'dim.txt' 
dim ← 2 ↑ dim
data ← ReadCSVInt 'data.txt'
data ← data > 0

board ← dim ⍴ data

a ← 201 nlife board

b ← life a

layout ← {
  arr ← ' o*'
  arr[⍵+1]
}

⎕ ← layout¨a

⍝ ⎕ ← 'Stable: '
⍝ s ← ∧/∧/a=b
⍝ ⎕ ← s+0
1
